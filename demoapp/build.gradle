apply plugin: 'mercadolibre.gradle.config.app'
apply plugin: 'com.google.gms.google-services'

android {


    defaultConfig {
        applicationId "$libraryGroupId" + ".demoapp"
        versionCode "$demoAppVersionCode".toInteger()
        versionName "$libraryVersion"

        multiDexEnabled true
        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }

    buildTypes {
        def config = { k -> "\"${project.properties.get(k)}\"" }
        debug {
            buildConfigField "String", "STORYBOOK_URL", config("storybook.url")
            buildConfigField "String", "A11Y_LANDING_URL", config("a11ylanding.url")
            applicationIdSuffix '.debug'
            minifyEnabled false
            aaptOptions {
                cruncherEnabled false
            }

            resValue("string", "andesui_demoapp_version", "v${libraryVersion}")
            signingConfig signingConfigs.debug
        }
        release {
            initWith(buildTypes.debug)

            // We need this in true to test after executing ProGuard to check our ProGuard custom rules.
            minifyEnabled true
            shrinkResources true

            applicationIdSuffix ''

            signingConfig signingConfigs.debug
        }
        mds {
            initWith(buildTypes.debug)

            // We need this in true to test after executing ProGuard to check our ProGuard custom rules.
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            if (project.hasProperty('mdsAppIdSuffix')) {
                applicationIdSuffix "${project.mdsAppIdSuffix}"
            }
            if (project.hasProperty('mdsVersionNameSuffix')) {
                versionNameSuffix "${project.mdsVersionNameSuffix}"
            }

            signingConfig signingConfigs.debug
        }
    }

    lintOptions {
        // TODO: Delete this once play services is updated. Now we need it because play services brings support library 26
        disable 'GradleCompatible'
    }

    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude("META-INF/*.kotlin_module")
    }

    dexOptions.preDexLibraries = true

    buildFeatures {
        viewBinding true
    }
}


dependencies {
    implementation project(':components')
    implementation project(':coachmark')

    implementation libs.misc.kotlinStdlibJdk7 
    implementation libs.misc.constraintLayout 
    implementation libs.misc.appCompat 
    implementation libs.misc.material 
    implementation libs.misc.coordinatorLayout 
    implementation (libs.meli.configurationMl.webkit) {
        exclude group: "com.google.android.gms"
    }
    implementation (libs.meli.mlwebkitComponents) {
        exclude group: "com.google.android.gms"
    }
    implementation (libs.meli.resourcesCore) {
        force true
    }
    implementation (libs.meli.uicomponentsResourceProvider) {
        force true
    }
    implementation (libs.meli.configurationMl.andes) {
        force true
    }

    implementation libs.misc.fresco 

    implementation libs.misc.glide 
    implementation libs.misc.markwonCore 

    implementation libs.misc.kotlinxCoroutinesCore 
    implementation libs.misc.kotlinxCoroutines 

    implementation platform (libs.misc.firebaseBom)
    implementation libs.misc.firebaseAnalytics 

    debugImplementation libs.misc.leakCanary 

}

/**
 * Based on https://stackoverflow.com/a/44218054/2823516
 */
copy {
    from file('../CHANGELOG.md')
    into 'src/main/res/raw'
    rename { String fileName ->
        fileName.replace("CHANGELOG.md", "andesui_demoapp_changelog.md")
    }
}