apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'kotlin-android'
apply from: "$rootDir/gradle/dexcount.gradle"

android {
    compileSdkVersion maxApiLevel.toInteger()
    buildToolsVersion androidBuildToolsVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "$libraryGroupId" + ".demoapp"
        minSdkVersion minApiLevel
        targetSdkVersion maxApiLevel
        versionCode "$demoAppVersionCode".toInteger()
        versionName "$libraryVersion"

        multiDexEnabled true
        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }

    buildTypes {
        def config = { k -> "\"${project.properties.get(k)}\"" }
        debug {
            buildConfigField "String", "STORYBOOK_URL", config("storybook.url")
            applicationIdSuffix '.debug'
            minifyEnabled false
            aaptOptions {
                cruncherEnabled false
            }

            resValue("string", "andesui_demoapp_version", "v${libraryVersion}")
            signingConfig signingConfigs.debug
        }
        release {
            initWith(buildTypes.debug)

            // We need this in true to test after executing ProGuard to check our ProGuard custom rules.
            minifyEnabled true
            shrinkResources true

            applicationIdSuffix ''

            signingConfig signingConfigs.debug
        }
        mds {
            initWith(buildTypes.debug)

            // We need this in true to test after executing ProGuard to check our ProGuard custom rules.
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            if (project.hasProperty('mdsAppIdSuffix')) {
                applicationIdSuffix "${project.mdsAppIdSuffix}"
            }
            if (project.hasProperty('mdsVersionNameSuffix')) {
                versionNameSuffix "${project.mdsVersionNameSuffix}"
            }

            signingConfig signingConfigs.debug
        }
    }

    lintOptions {
        // TODO: Delete this once play services is updated. Now we need it because play services brings support library 26
        disable 'GradleCompatible'
    }

    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude("META-INF/*.kotlin_module")
    }

    dexOptions.preDexLibraries = true

    buildFeatures {
        viewBinding true
    }
}


dependencies {
    implementation project(':components')
    implementation project(':coachmark')

    implementation prod.kotlinStdlib
    implementation prod.supportConstraintLayout
    implementation prod.supportLibraryAppCompatV7
    implementation prod.supportLibraryDesign
    implementation showcase.coordinatorLayout
    implementation(showcase.webkitConfigProvider) {
        exclude group: "com.google.android.gms"
    }
    implementation(showcase.webkit) {
        exclude group: "com.google.android.gms"
    }
    implementation(showcase.odr) {
        force true
    }
    implementation(showcase.uiComponents) {
        force true
    }

    implementation prod.fresco

    implementation showcase.glide
    implementation showcase.markwon

    implementation coroutines.core
    implementation coroutines.android

    implementation platform(firebase.bom)
    implementation firebase.analytics

    debugImplementation showcase.leakCanary

}

/**
 * Based on https://stackoverflow.com/a/44218054/2823516
 */
copy {
    from file('../CHANGELOG.md')
    into 'src/main/res/raw'
    rename { String fileName ->
        fileName.replace("CHANGELOG.md", "andesui_demoapp_changelog.md")
    }
}